---
title: "code"
output: 
  html_document:
    toc: true
    toc_float: true
    theme: cerulean
---

Code:
/*create sas libraries and assoicate them with pathways*/
LIBNAME input '/folders/myfolders/sas_code_sample/input';
LIBNAME output '/folders/myfolders/sas_code_sample/output';

/*import dataset echocardiogram.csv*/
PROC IMPORT DATAFILE='/folders/myfolders/sas_code_sample/input/echocardiogram.csv'
	DBMS=CSV
	OUT=input.echocardiogram;
	GETNAMES=YES;
RUN;

/*data manipulation*/
DATA input.echocardiogram;
	SET input.echocardiogram;
	RENAME pericardialeffusion=pericardial_effusion 
	       fractionalshortening=fractional_shortening
	       wallmotion_index=wall_motion_index;
	DROP mult name group aliveat1 wallmotion_score;
RUN;

/*Descriptive Statistics*/
ODS PDF file="/folders/myfolders/sas_code_sample/output/descriptive_statistics.pdf";
/*check missing value*/
PROC MEANS DATA=input.echocardiogram n nmiss;
	TITLE "Number and Percentage of Missing Data";
RUN;

/*calulate max, min, Q1,Q3,mean, median of continuous variables*/
PROC TABULATE DATA=input.echocardiogram;
	VAR survival age fractional_shortening epss lvdd wall_motion_index;
	CLASS alive;
	TABLES (survival age fractional_shortening epss lvdd wall_motion_index)*(mean median min max Q1 Q3), alive;
	TITLE "Descriptive Statistics of Continuous Variables";
RUN;

/*calulate frequency of discrete variables*/
PROC FREQ DATA=input.echocardiogram;
	TABLE pericardial_effusion*alive;
	TITLE "Descriptive Statistics of Dummy Variables";
RUN;
ODS PDF CLOSE;

/*build sas macro function of graphing boxplot and export result*/
%MACRO boxplot(var=,database=);
	%LET var_no_underline=%SYSFUNC(TRANWRD(&var,_, ));
	TITLE "Box Plot for &var_no_underline";
	PROC SGPLOT DATA=input.&database;
		VBOX &var;
	RUN;
%mend;

/*build sas macro function of graphing bar chart and export result*/
%MACRO barplot(var=,database=);
	%LET var_no_underline=%SYSFUNC(TRANWRD(&var,_, ));
	TITLE "Bar Chart for &var_no_underline";
	PROC SGPLOT DATA=input.&database;
		VBAR &var;
	RUN;
%mend;

/*use sas macro to graph boxplot of all variables excluding alive and pericardial_effusion*/
ODS PDF file="/folders/myfolders/sas_code_sample/output/data_checking.pdf";
%boxplot(var=survival,database=echocardiogram)
%boxplot(var=age,database=echocardiogram)
%boxplot(var=fractional_shortening,database=echocardiogram)

%boxplot(var=epss,database=echocardiogram)
%boxplot(var=lvdd,database=echocardiogram)
%boxplot(var=wall_motion_index,database=echocardiogram)

/*use sas macro to graph bar chart of binary variables alive and pericardial_effusion*/
%barplot(var=alive,database=echocardiogram)
%barplot(var=pericardial_effusion,database=echocardiogram)
ODS PDF CLOSE;

/*remove unreasonable value and observation with missing survival time*/
DATA input.echocardiogram;
	SET input.echocardiogram;
	IF pericardial_effusion>1 THEN pericardial_effusion=.;
	IF survival^=. AND alive^=. THEN output;
RUN;
	
/*Bivariate Analysis*/
/*correlation matrix and Multicollinearity Diagnostics*/
ODS PDF file="/folders/myfolders/sas_code_sample/output/multicollinearity_diagnostics.pdf";
ODS SELECT Corr.PearsonCorr;
PROC CORR DATA=input.echocardiogram plots(maxpoints=none)=matrix(histogram);
	TITLE "Correlation Matrix of variables and outcome";
	VAR survival alive age pericardial_effusion fractional_shortening epss lvdd wall_motion_index;
	WITH survival alive age pericardial_effusion fractional_shortening epss lvdd wall_motion_index;
RUN;

ODS SELECT ParameterEstimates;
PROC REG data=input.echocardiogram;
	MODEL survival = age pericardial_effusion fractional_shortening epss lvdd wall_motion_index  / vif tol; 
	TITLE 'Multicollinearity Diagnostics';
RUN;

/*association between epss and lvdd*/
TITLE 'Scatter Plot of epss verse lvdd';
PROC SGPLOT DATA=input.echocardiogram;
	SCATTER x=lvdd y=epss;
	LOESS x=lvdd y=epss;
	XAXIS LABEL="left ventricular end-diastolic dimension";
	YAXIS LABEL="E-point septal separation";
RUN;

/*association between epss and wall motion index*/
TITLE 'Scatter Plot of epss verse wall motion index';
PROC SGPLOT DATA=input.echocardiogram;
	SCATTER x=wall_motion_index y=epss;
	REG x=wall_motion_index y=epss;
	XAXIS LABEL="equals wall-motion-score divided by number of segments seen";
	YAXIS LABEL="E-point septal separation";
RUN;
ODS PDF CLOSE;

/*plot histogram of continuous variables for sample with alive=1 verse alive=0*/
PROC FORMAT;
	VALUE alive_format 0 = "dead"
					   1 = "alive";
%macro histogram(var=,database=);
	%LET var_no_underline=%SYSFUNC(TRANWRD(&var,_, ));
	PROC SGPLOT data=input.&database;
		TITLE "Histogram of &var_no_underline Group by Survival status";
		HISTOGRAM &var / name="label1" group=alive transparency=0.5;
		DENSITY &var / type=kernel group=alive;
		XAXIS label="&var_no_underline";
		FORMAT alive alive_format.;
		KEYLEGEND "label1" / across=1 position=TopRight location=Inside;
	RUN;
%mend;

ODS PDF file="/folders/myfolders/sas_code_sample/output/bivariate_analysis.pdf";
%histogram(var=survival,database=echocardiogram)
%histogram(var=age,database=echocardiogram)
%histogram(var=pericardial_effusion,database=echocardiogram)
%histogram(var=fractional_shortening,database=echocardiogram)

%histogram(var=epss,database=echocardiogram)
%histogram(var=lvdd,database=echocardiogram)
%histogram(var=wall_motion_index,database=echocardiogram)

/*plot bar chart of dummy variables for sample with alive=1 verse alive=0*/
%LET var_no_underline=%SYSFUNC(TRANWRD(pericardial_effusion,_, ));
PROC SGPLOT data=input.echocardiogram;
	TITLE "Bar Chart of pericardial effusion Group by Survival status";
	VBAR pericardial_effusion / name="label1" group=alive transparency=0.5 groupdisplay=cluster;
	XAXIS label="&var_no_underline";
	FORMAT alive alive_format.;
	KEYLEGEND "label1" / across=1 position=TopRight location=Inside;
RUN;
ODS PDF CLOSE;

/*model fitting and hypothesis test*/
/*create variable censor*/
DATA input.echocardiogram;
	SET input.echocardiogram;
	censor=1-alive;
RUN;

/*plot and export Kaplan-Meier estimates of survival rate*/
ODS PDF file="/folders/myfolders/sas_code_sample/output/survival_analysis.pdf";
ODS SELECT Lifetest.Stratum1.SurvivalPlot;
TITLE "Kaplan-Meier estimates of survival rate";
PROC LIFETEST DATA=input.echocardiogram
	PLOTS=survival(cb) OUTS=Kaplan_Meier_estimate;
	TIME survival*censor(0);
RUN;

/*fit cox proportional hazard model*/
ODS SELECT PHReg.GlobalTests PHReg.ModelANOVA PHReg.ParameterEstimates PHReg.SurvivalPlot CumulativeResiduals ScoreProcess ProportionalHazardsSupTest; 
TITLE "Cox Proportional Hazard Model";
PROC PHREG data = input.echocardiogram plots=survival;
	CLASS pericardial_effusion;
	MODEL survival*censor(0) = age pericardial_effusion fractional_shortening epss lvdd wall_motion_index;
	ASSESS var=(age fractional_shortening epss lvdd wall_motion_index) ph / resample;
RUN;
ODS PDF CLOSE;
